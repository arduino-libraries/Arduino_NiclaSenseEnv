/**
 * This example shows how to print the device information of the Nicla Sense Env, 
 * how to disable all sensors and how to reset the device or put it to sleep.
 * 
 * Initial author: Sebastian Romero (s.romero@arduino.cc)
 */

#include "Arduino_NiclaSenseEnv.h"

void printDeviceInfo(NiclaSenseEnv& device) {
    Serial.print("ðŸ”Œ Device (0x");
    Serial.print(device.deviceAddress(), HEX);
    Serial.println(") connected.");
    Serial.print("ðŸ”¢ Serial number: ");
    Serial.println(device.serialNumber());
    Serial.print("ðŸ”¢ Product ID: ");
    Serial.println(device.productID());
    Serial.print("ðŸ”¢ Software revision: ");
    Serial.println(device.softwareRevision());
    Serial.print("ðŸ”¢ Baud rate: ");
    Serial.println(device.UARTBaudRate());
    Serial.print("ðŸ”¢ CSV delimiter: ");
    Serial.println(device.CSVDelimiter());
    Serial.print("ðŸ”§ Debugging enabled: ");
    Serial.println(device.isDebuggingEnabled() ? "true" : "false");
    Serial.print("ðŸ”§ CSV output enabled: ");
    Serial.println(device.isUARTCSVOutputEnabled() ? "true" : "false");
}

void setup() {
    Serial.begin(115200);
    while (!Serial) {
        // Wait for Serial to be ready
    }

    NiclaSenseEnv device;

    if (device.begin()) {
        printDeviceInfo(device);

        // Enable debugging and CSV output if desired
        // device.setDebuggingEnabled(true);
        // device.setUARTCSVOutputEnabled(true);

        // Disable all sensors
        Serial.println("ðŸ™… Disabling all sensors...");
        device.temperatureHumiditySensor().setEnabled(false);
        device.indoorAirQualitySensor().setEnabled(false);
        device.outdoorAirQualitySensor().setEnabled(false);

        Serial.print("ðŸ”§ Temperature sensor enabled: ");
        Serial.println(device.temperatureHumiditySensor().enabled() ? "true" : "false");
        Serial.print("ðŸ”§ Indoor air quality sensor enabled: ");
        Serial.println(device.indoorAirQualitySensor().enabled() ? "true" : "false");
        Serial.print("ðŸ”§ Outdoor air quality sensor enabled: ");
        Serial.println(device.outdoorAirQualitySensor().enabled() ? "true" : "false");

        // Reset sensors to default state by resetting the device
        Serial.println("ðŸ”„ Resetting device...");
        device.reset();
        delay(2000);

        Serial.print("ðŸ”§ Temperature sensor enabled: ");
        Serial.println(device.temperatureHumiditySensor().enabled() ? "true" : "false");
        Serial.print("ðŸ”§ Indoor air quality sensor enabled: ");
        Serial.println(device.indoorAirQualitySensor().enabled() ? "true" : "false");
        Serial.print("ðŸ”§ Outdoor air quality sensor enabled: ");
        Serial.println(device.outdoorAirQualitySensor().enabled() ? "true" : "false");

        // Go to sleep. Device can only be woken up by pressing the reset button
        Serial.println("ðŸ’¤ Going to sleep...");
        device.deepSleep();
    } else {
        Serial.println("ðŸ¤· Device could not be found. Please double-check the wiring.");
    }
}

void loop() {
    // Code in the loop if needed
}
